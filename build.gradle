plugins {
    id 'org.springframework.boot' version '2.4.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.8' // (1)
    id 'java'
}

group = 'co.kr.tmaxai'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

// (2)
ext {
    set('snippetsDir', file("build/generated-snippets")) 
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc' // (3)
}

test {
    outputs.dir snippetsDir // (4)
    useJUnitPlatform()
}

// (5)
asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

// (6)
asciidoctor.doFirst {
    println "====start asciidocter"
    delete file('src/main/resources/static/docs')
}

// (7)
asciidoctor.doLast {
    println "====finish asciidoctor"
}

// (8)
task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/asciidoc/html5")
    into file("src/main/resources/static/docs")
}

// (9)
build {
    dependsOn copyDocument
}

// (10)
bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}